options:
    prefix: &8[&aSpawn&8] &7
    successfully: &8[&a&l!&8] &r&a
    error: &8[&c&l!&8] &r&c

# -------------------------
# Comando /setspawn
# -------------------------
command /setspawn:
    usage: /setspawn
    permission: admin.setspawn
    permission message: {@error}No tienes permiso para usar este comando.
    trigger:
        set {spawn.location} to location of player
        send "{@prefix}&aSpawn definido correctamente."
        play sound "entity.player.levelup" to player

# -------------------------
# Comando /spawn con permisos y bloqueo por combate
# -------------------------
command /spawn:
    usage: /spawn
    permission: essentials.spawn
    permission message: {@error}No tienes permiso para usar /spawn.
    trigger:
        if {spawn.location} is not set:
            send "{@error}El spawn aún no está definido."
            stop

        if {combate.%uuid of player%} is set:
            send "{@error}¡No puedes usar /spawn mientras estás en combate!"
            stop

        set {_loc} to location of player
        loop 5 times:
            set {_t} to 6 - loop-number
            send action bar "&aTeleportando en %{_t}% segundos. &c&l¡No te muevas!" to player
            if location of player != {_loc}:
                send action bar "{@error}¡Teletransporte cancelado por moverte!"
                stop
            wait 1 second

        teleport player to {spawn.location}
        play sound "entity.player.levelup" to player
        send action bar "{@successfully}&aFuiste teletransportado al spawn."

# -------------------------
# Teleport al spawn en primer ingreso, respawn y entrada
# -------------------------
on first join:
    broadcast "&e%player% ha entrado por primera vez, bienvenido a la modalidad &aSurvival Parcelas&e, usa &6/menu &epara ver las funcionalidades del servidor."
    set join message to ""
    if {spawn.location} is set:
        teleport player to {spawn.location}

on join:
    set join message to ""
    broadcast "&8- &7(&a&l+&7) &r&f%player%"
    if {spawn.location} is set:
        teleport player to {spawn.location}

on quit:
    set quit message to ""
    broadcast "&8- &7(&c&l-&7) &r&f%player%"

on respawn:
    if {spawn.location} is set:
        set respawn location to {spawn.location}
